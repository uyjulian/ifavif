# Copyright (c) ifavif developers

cmake_minimum_required(VERSION 3.0)
project(ifavif)

if((NOT DEFINED CMAKE_BUILD_TYPE) OR (NOT CMAKE_BUILD_TYPE) OR (CMAKE_BUILD_TYPE STREQUAL ""))
set(CMAKE_BUILD_TYPE RelWithDebInfo)
endif()

set(DEPENDENCY_OUTPUT_DIRECTORY ${CMAKE_CURRENT_BINARY_DIR}/build-libraries)
file(MAKE_DIRECTORY ${DEPENDENCY_OUTPUT_DIRECTORY}/include)

if(NOT DEFINED TOOL_TRIPLET_PREFIX)
if(${CMAKE_SYSTEM_PROCESSOR} STREQUAL i686)
set(TOOL_TRIPLET_PREFIX i686-w64-mingw32-)
endif()
if(${CMAKE_SYSTEM_PROCESSOR} STREQUAL amd64)
set(TOOL_TRIPLET_PREFIX x86_64-w64-mingw32-)
endif()
if(${CMAKE_SYSTEM_PROCESSOR} STREQUAL arm)
set(TOOL_TRIPLET_PREFIX armv7-w64-mingw32-)
endif()
if(${CMAKE_SYSTEM_PROCESSOR} STREQUAL arm64)
set(TOOL_TRIPLET_PREFIX aarch64-w64-mingw32-)
endif()
if(NOT DEFINED TOOL_TRIPLET_PREFIX)
set(TOOL_TRIPLET_PREFIX i686-w64-mingw32-)
endif()
endif()
string(REGEX REPLACE "-$" "" TOOL_TRIPLET_PREFIX_NOTRAILINGDASH "${TOOL_TRIPLET_PREFIX}")
set(MESON_CROSS_C ${CMAKE_C_COMPILER})
set(MESON_CROSS_CPP ${CMAKE_CXX_COMPILER})
set(MESON_CROSS_AR ${CMAKE_AR})
set(MESON_CROSS_STRIP ${TOOL_TRIPLET_PREFIX}strip)
set(MESON_CROSS_WINDRES ${TOOL_TRIPLET_PREFIX}windres)
set(MESON_CROSS_OBJCOPY ${TOOL_TRIPLET_PREFIX}objcopy)
if(NOT DEFINED MESON_CROSS_CPU_FAMILY)
if(${CMAKE_SYSTEM_PROCESSOR} STREQUAL i686)
set(MESON_CROSS_CPU_FAMILY x86)
endif()
if(${CMAKE_SYSTEM_PROCESSOR} STREQUAL amd64)
set(MESON_CROSS_CPU_FAMILY x86_64)
endif()
if(${CMAKE_SYSTEM_PROCESSOR} STREQUAL arm)
set(MESON_CROSS_CPU_FAMILY arm)
endif()
if(${CMAKE_SYSTEM_PROCESSOR} STREQUAL arm64)
set(MESON_CROSS_CPU_FAMILY aarch64)
endif()
if(NOT DEFINED MESON_CROSS_CPU_FAMILY)
set(MESON_CROSS_CPU_FAMILY x86)
endif()
endif()
if(NOT DEFINED MESON_CROSS_CPU)
if(${CMAKE_SYSTEM_PROCESSOR} STREQUAL i686)
set(MESON_CROSS_CPU i686)
endif()
if(${CMAKE_SYSTEM_PROCESSOR} STREQUAL amd64)
set(MESON_CROSS_CPU amd64)
endif()
if(${CMAKE_SYSTEM_PROCESSOR} STREQUAL arm)
set(MESON_CROSS_CPU armv7)
endif()
if(${CMAKE_SYSTEM_PROCESSOR} STREQUAL arm64)
set(MESON_CROSS_CPU aarch64)
endif()
if(NOT DEFINED MESON_CROSS_CPU)
set(MESON_CROSS_CPU i686)
endif()
endif()
configure_file(${CMAKE_CURRENT_SOURCE_DIR}/meson_cross.ini.cmakein ${CMAKE_CURRENT_BINARY_DIR}/meson_cross.ini)

if((NOT DEFINED MY_LIBRARY_PROCESSOR) OR (NOT MY_LIBRARY_PROCESSOR) OR (MY_LIBRARY_PROCESSOR STREQUAL ""))
set(MY_LIBRARY_PROCESSOR ${CMAKE_SYSTEM_PROCESSOR})
if(${CMAKE_SYSTEM_PROCESSOR} STREQUAL "i686")
set(MY_LIBRARY_PROCESSOR intel32)
endif()
if(${CMAKE_SYSTEM_PROCESSOR} STREQUAL "amd64")
set(MY_LIBRARY_PROCESSOR intel64)
endif()
if(${CMAKE_SYSTEM_PROCESSOR} STREQUAL "arm")
set(MY_LIBRARY_PROCESSOR arm32)
endif()
if(${CMAKE_SYSTEM_PROCESSOR} STREQUAL "arm64")
set(MY_LIBRARY_PROCESSOR arm64)
endif()
endif()

include(ExternalProject)
ExternalProject_Add(libavif-build
	URL https://github.com/AOMediaCodec/libavif/archive/refs/tags/v1.2.0.tar.gz
	URL_HASH SHA256=2182f4900d1a9617cee89746922a58dd825f2a3547f23907b8d78dc3685f7d8c
	DOWNLOAD_EXTRACT_TIMESTAMP TRUE
	CONFIGURE_COMMAND ${CMAKE_COMMAND} -E env
		"PKG_CONFIG_PATH=${DEPENDENCY_OUTPUT_DIRECTORY}/lib/pkgconfig"
		${CMAKE_COMMAND}
			-B <BINARY_DIR>
			-S <SOURCE_DIR>
			-DCMAKE_SYSTEM_NAME=Windows
			-DCMAKE_SYSTEM_PROCESSOR=${CMAKE_SYSTEM_PROCESSOR}
			-DCMAKE_FIND_ROOT_PATH=/dev/null
			-DCMAKE_FIND_ROOT_PATH_MODE_PROGRAM=NEVER
			-DCMAKE_FIND_ROOT_PATH_MODE_LIBRARY=ONLY
			-DCMAKE_FIND_ROOT_PATH_MODE_INCLUDE=ONLY
			-DCMAKE_FIND_ROOT_PATH_MODE_PACKAGE=ONLY
			-DCMAKE_DISABLE_FIND_PACKAGE_PkgConfig=TRUE
			-DCMAKE_C_COMPILER=${CMAKE_C_COMPILER}
			-DCMAKE_CXX_COMPILER=${CMAKE_CXX_COMPILER}
			-DCMAKE_RC_COMPILER=${CMAKE_RC_COMPILER}
			-DCMAKE_INSTALL_PREFIX=${DEPENDENCY_OUTPUT_DIRECTORY}
			-DCMAKE_BUILD_TYPE=Release
			"-DCROSS_FILE=${CMAKE_CURRENT_BINARY_DIR}/meson_cross.ini"
			-DAVIF_CODEC_DAV1D=LOCAL
			-DBUILD_SHARED_LIBS=OFF
			-DAVIF_LIBYUV=LOCAL
			"-DDAV1D_LIBRARIES=${DEPENDENCY_OUTPUT_DIRECTORY}/lib/libdav1d.a"
			"-DDAV1D_LIBRARY=${DEPENDENCY_OUTPUT_DIRECTORY}/lib/libdav1d.a"
			"-DDAV1D_INCLUDE_DIR=${DEPENDENCY_OUTPUT_DIRECTORY}/include/"
	BUILD_COMMAND ${CMAKE_COMMAND} --build <BINARY_DIR> && ${CMAKE_COMMAND} --build <BINARY_DIR> --target install
	BUILD_BYPRODUCTS ${DEPENDENCY_OUTPUT_DIRECTORY}/lib/libavif.a
	INSTALL_COMMAND ""
)
add_library(libavif STATIC IMPORTED)
set_property(TARGET libavif PROPERTY IMPORTED_LOCATION "${DEPENDENCY_OUTPUT_DIRECTORY}/lib/libavif.a")
add_dependencies(libavif libavif-build)
target_include_directories(libavif INTERFACE "${DEPENDENCY_OUTPUT_DIRECTORY}/include")

if((NOT DEFINED CUR_GIT_TAG) OR (NOT CUR_GIT_TAG) OR (CUR_GIT_TAG STREQUAL ""))
	set(CUR_GIT_TAG Unknown)
	find_package(Git)
	if(GIT_FOUND)
		if(EXISTS ${CMAKE_CURRENT_SOURCE_DIR}/.git)
			execute_process(COMMAND ${GIT_EXECUTABLE} describe --abbrev=0 --tags
			WORKING_DIRECTORY ${CMAKE_SOURCE_DIR}
			OUTPUT_VARIABLE TMP_GIT_TAG
			RESULT_VARIABLE TMP_RES
			OUTPUT_STRIP_TRAILING_WHITESPACE)
			if(TMP_RES EQUAL "0")
				set(CUR_GIT_TAG ${TMP_GIT_TAG})
			endif()
		endif()
	endif()
endif()

set(MY_LIBRARY_RC_COMMENTS "Source code for the latest version of this product is located on the World Wide Web at https://github.com/uyjulian/ifavif")
set(MY_LIBRARY_RC_FILEDESCRIPTION "AVIF Plugin for Susie Image Viewer")
set(MY_LIBRARY_RC_FILEVERSION "${CUR_GIT_TAG}")
set(MY_LIBRARY_RC_INTERNALNAME "ifavif")
set(MY_LIBRARY_RC_LEGALCOPYRIGHT "Copyright (C) 2019-2019 Julian Uy; This product is licensed under the MIT license.")
set(MY_LIBRARY_RC_ORIGINALFILENAME "ifavif.spi")
set(MY_LIBRARY_RC_PRODUCTNAME "AVIF Plugin for Susie Image Viewer")
set(MY_LIBRARY_RC_PRODUCTVERSION "${CUR_GIT_TAG}")
configure_file(${CMAKE_CURRENT_SOURCE_DIR}/dllres.rc.cmakein ${CMAKE_CURRENT_BINARY_DIR}/dllres.rc)
set(MY_LIBRARY_NAME ${PROJECT_NAME})
list(APPEND MY_LIBRARY_DEFINITIONS
	-DUNICODE
	-D_UNICODE
	-DNDEBUG
	-DWIN32
	-D_WIN32
	-D_WINDOWS 
)
list(APPEND MY_LIBRARY_CFLAGS
	-Wno-unused-value
	-Wno-format
	-flto
)
list(APPEND MY_LIBRARY_CXXFLAGS
	${MY_LIBRARY_CFLAGS}
	-fpermissive
)
list(APPEND MY_LIBRARY_LDFLAGS
	-static
	-static-libstdc++
	-static-libgcc
	-municode
	-Wl,--subsystem,windows
	-Wl,--kill-at
	-fPIC
)
list(APPEND MY_LIBRARY_LIBS
	libavif
)
list(APPEND MY_LIBRARY_SRC
	extractor.c
	spi00in.c
	${CMAKE_CURRENT_BINARY_DIR}/dllres.rc
)
list(APPEND MY_LIBRARY_INC
	${CMAKE_CURRENT_SOURCE_DIR}
)

add_library(${MY_LIBRARY_NAME} MODULE)
if(NOT ((${CMAKE_SYSTEM_PROCESSOR} STREQUAL "i686") OR (${CMAKE_SYSTEM_PROCESSOR} STREQUAL "amd64")))
set_target_properties(${MY_LIBRARY_NAME} PROPERTIES OUTPUT_NAME ${MY_LIBRARY_NAME}_${MY_LIBRARY_PROCESSOR})
endif()
set_target_properties(${MY_LIBRARY_NAME} PROPERTIES PREFIX "")
if(${CMAKE_SYSTEM_PROCESSOR} STREQUAL "amd64")
set_target_properties(${MY_LIBRARY_NAME} PROPERTIES SUFFIX ".sph")
else()
set_target_properties(${MY_LIBRARY_NAME} PROPERTIES SUFFIX ".spi")
endif()
set_target_properties(${MY_LIBRARY_NAME} PROPERTIES POSITION_INDEPENDENT_CODE TRUE)
target_sources(${MY_LIBRARY_NAME} PRIVATE ${MY_LIBRARY_SRC})
target_compile_options(${MY_LIBRARY_NAME} PUBLIC $<$<COMPILE_LANGUAGE:C>:${MY_LIBRARY_CFLAGS}>)
target_compile_options(${MY_LIBRARY_NAME} PUBLIC $<$<COMPILE_LANGUAGE:CXX>:${MY_LIBRARY_CXXFLAGS}>)
target_compile_definitions(${MY_LIBRARY_NAME} PUBLIC ${MY_LIBRARY_DEFINITIONS})
target_include_directories(${MY_LIBRARY_NAME} PUBLIC ${MY_LIBRARY_INC})
if(${CMAKE_VERSION} VERSION_GREATER_EQUAL "3.13.0")
target_link_options(${MY_LIBRARY_NAME} PUBLIC ${MY_LIBRARY_LDFLAGS})
endif()
target_link_libraries(${MY_LIBRARY_NAME} ${MY_LIBRARY_LIBS})

if((NOT DEFINED MY_ARCHIVE_NAME) OR (NOT MY_ARCHIVE_NAME) OR (MY_ARCHIVE_NAME STREQUAL ""))
if((NOT DEFINED USE_ARCHIVE_HAS_GIT_TAG) OR (NOT USE_ARCHIVE_HAS_GIT_TAG) OR (USE_ARCHIVE_HAS_GIT_TAG STREQUAL ""))
set(USE_ARCHIVE_HAS_GIT_TAG FALSE)
endif()
if(USE_ARCHIVE_HAS_GIT_TAG)
set(MY_ARCHIVE_NAME ${MY_LIBRARY_NAME}.${MY_LIBRARY_PROCESSOR}.${CUR_GIT_TAG}.7z)
else()
set(MY_ARCHIVE_NAME ${MY_LIBRARY_NAME}.${MY_LIBRARY_PROCESSOR}.7z)
endif()
endif()
add_custom_command(
	OUTPUT ${CMAKE_CURRENT_BINARY_DIR}/${MY_ARCHIVE_NAME}
	COMMAND rm -f ${CMAKE_CURRENT_BINARY_DIR}/${MY_ARCHIVE_NAME} && 7z a ${CMAKE_CURRENT_BINARY_DIR}/${MY_ARCHIVE_NAME} $<TARGET_FILE:${MY_LIBRARY_NAME}>
	DEPENDS ${MY_LIBRARY_NAME}
)
add_custom_target(archive
	DEPENDS ${MY_ARCHIVE_NAME}
)
